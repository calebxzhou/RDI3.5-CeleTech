plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
	alias libs.plugins.quilt.loom.quiltflower
}

archivesBaseName = project.archives_base_name
version = project.version
group = project.maven_group

configurations {
	//includeModImplementation
	includeImplementation
	include.extendsFrom includeImplementation
	//include.extendsFrom includeModImplementation
	implementation.extendsFrom includeImplementation
	//modImplementation.extendsFrom includeModImplementation
}
//不用mc自带的apache http，一会引入一个新版的
configurations.implementation {
	exclude group: 'org.apache.httpcomponents', module: 'httpclient'
	exclude group: 'org.apache.httpcomponents', module: 'httpcore'
}

repositories {
	mavenCentral()
	maven {url="https://maven.enginehub.org/repo/"}
	maven { url "https://maven.dblsaiko.net/" }
	maven { url "https://maven.fabricmc.net/" }
	maven { url 'https://maven.blamejared.com' }
	maven { url "https://maven.shedaniel.me/" }
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.terraformersmc.com/releases' }
	maven { url = "https://maven.terraformersmc.com/" }
	maven { url "https://ladysnake.jfrog.io/artifactory/mods" }
	maven { url = "https://dvs1.progwml6.com/files/maven/" }
	maven { url = "https://modmaven.dev" }
	maven { url = 'https://maven.nucleoid.xyz/' }
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

// All the dependencies are declared at gradle/libs.version.toml and referenced with "libs.<id>"
// See https://docs.gradle.org/current/userguide/platforms.html for information on how version catalogs work.
dependencies {
	minecraft libs.minecraft
	mappings loom.layered {
		addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2")
		officialMojangMappings() // Uncomment if you want to use Mojang mappings as your primary mappings, falling back on QM for parameters and Javadocs
		parchment("org.parchmentmc.data:parchment-1.19.2:2022.09.04@zip")
	}
	modImplementation libs.quilt.loader
	modImplementation libs.quilted.fabric.api
	//新版的 http client，载入到mod包里
	includeImplementation( 'com.squareup.okhttp3:okhttp:4.10.0' )
	includeImplementation( 'com.squareup.okio:okio:3.0.0' )
	includeImplementation( 'com.squareup.okio:okio-jvm:3.0.0' )
	modImplementation("maven.modrinth:spark:1.9.26-fabric")
	modImplementation("maven.modrinth:lazydfu:0.1.3")
	modImplementation("maven.modrinth:lithium:mc1.19.2-0.8.3")
	//ferrite core 节省内存
	modImplementation("maven.modrinth:ferrite-core:5.0.0-fabric")

}

processResources {
	inputs.property "version", version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	// Still required by IDEs such as Eclipse and Visual Studio Code
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
	// Uncomment this line to generate them.
	// withJavadocJar()
}

// If you plan to use a different file for the license, don't forget to change the file name here!
jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
loom {
	accessWidenerPath = file("src/main/resources/rdict3.accesswidener")
}
